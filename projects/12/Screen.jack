// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
  
    static Array SCREEN;
    static bool isBlack;

    /** Initializes the Screen. */
    function void init() {
      let SCREEN = 16384;
      do Screen.setColor(true);
      return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
      var int addr, lastAddr;
      let addr = SCREEN;
      let lastAddr = SCREEN + 8192;
      
      while (addr < lastAddr) {
        do Memory.poke(addr, 0);
        let addr = addr + 1;
      }
      
      return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
      let isBlack = b;
      return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
      var int address, value, bit, mask;
      let address = (32*y)+(x/16);
      let value = Memory.peek(SCREEN + address);
   
      /** set the (x%16)th bit of value to the current color */
      let bit = Math.mod(x, 16);
      let mask = Math.getTwoToThe(bit);
      
      if (isBlack = true) {
        let mask = value | mask;
      }
      else {
        let mask = value & (~mask);
      }
      do Memory.poke(SCREEN + address, mask);
      return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
      if ((y1 = y2) & (x2 > x1)) {
        do Screen.drawHorizontalLine(x1, x2, y1);
      }
      if ((y1 = y2) & (x2 < x1)) {
        do Screen.drawHorizontalLine(x2, x1, y1);
      }
      if ((x1 = x2) & (y2 > y1)) {
        do Screen.drawVerticalLine(x1, y1, y2);
      }
      if ((x1 = x2) & (y2 < y1)) {
        do Screen.drawVerticalLine(x1, y2, y1);
      }
      if ((y2 > y1) & (x2 > x1)) { /* top left to bottom right */
        do Screen.drawTopLeftToBttmRightLine(x1, y1, x2, y2);
      }
      if ((y2 < y1) & (x2 > x1)) { /* bottom left to top right */
        do Screen.drawBttmLeftToTopRightLine(x1, y1, x2, y2);
      }
      if ((y2 > y1) & (x2 < x1)) { /* bottom right to top left */
        do Screen.drawBttmLeftToTopRightLine(x2, y2, x1, y1);
      }
      if ((y2 < y1) & (x2 < x1)) { /* top right to bottom left */
        do Screen.drawTopLeftToBttmRightLine(x2, y2, x1, y1);
      }
      return;
    }
    
    /** Draws horizontal lines */
    function void drawHorizontalLine(int x1, int x2, int y) {
      var int it, end;
      let it = x1;
      let end = x2;
      
      while (it < end) {
        do Screen.drawPixel(it, y);
        let it = it+1;
      }
      return;
    }

    /** Draws diagonal lines */
    function void drawTopLeftToBttmRightLine(int x1, int y1, int x2, int y2) {
      var int itx, ity, dx, dy, diff;
      let itx = 0;
      let ity = 0;
      let dx = x2 - x1;
      let dy = y2 - y1;
      let diff = 0;
      
      while((~(itx > dx)) & (~(ity > dy))) {
        do Screen.drawPixel((x1+itx), y1+ity);
        if (diff < 0) {
          let itx = itx + 1;
          let diff = diff + dy;
        }
        else {
          let ity = ity + 1;
          let diff = diff - dx;
        }
      }
      
      return;
    }
    
    /** Draws bottom left to top right lines */
    function void drawBttmLeftToTopRightLine(int x1, int y1, int x2, int y2) {
      var int itx, ity, dx, dy, diff;
      let itx = 0;
      let ity = 0;
      let dx = x2 - x1;
      let dy = y2 - y1;
      let diff = 0;
      
      while((~(itx > dx)) & (~(ity < dy))) {
        do Screen.drawPixel((x1+itx), y1+ity);
        if (diff > 0) {
          let itx = itx + 1;
          let diff = diff + dy;
        }
        else {
          let ity = ity - 1;
          let diff = diff + dx;
        }
      }
      
      return;
    }
    
    /** Draws verticallines */
    function void drawVerticalLine(int x, int y1, int y2) {
      var int it, end;
      let it = y1;
      let end = y2;
      
      while (it < end) {
        do Screen.drawPixel(x, it);
        let it = it+1;
      }return;
    }    
    
    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
      var int it, end;
      let it = y1;
      let end = y2;
     
      while (it < end) {
        do Screen.drawHorizontalLine(x1, x2, it);
        let it = it+1;
      }
      
      return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
      var int ity;
      let ity = -r;
      
      while (ity < r) {
        do Screen.drawLine((x-Math.sqrt((r*r)-(ity*ity))), (y+ity), (x+Math.sqrt((r*r)-(ity*ity))), (y+ity));
        let ity = ity + 1;
      }
      
      return;
    }
}










